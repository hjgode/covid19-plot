# version 2 with plot of deaths
# gnuplot -e "filename='time_series_germany_sorted.csv';outfile='germany.png';mytitle='Germany'" plot_covid.gplot

if (!exists("filename")) filename='time_series_germany_sorted.csv'
if (!exists("outfile")) outfile='germany.png'
if (!exists("mytitle")) mytitle='Germany'
set title mytitle
set xdata time 
set timefmt "%m/%d/%Y"
set style data lines
set terminal png size 1000,400
set autoscale y
set output outfile
set datafile separator ","
set xrange ["3/1/20":*]
set y2tics nomirror
set y2tics
set y2range [0:10000]
set y2label 'Delta'
# set legend position
set key left bottom
# line styles
set style line 1 linetype 2 linewidth 2 pt 3 ps 0.5 lc rgb "orange"
#"red"
set style line 2 linetype 2 linewidth 2 pt 3 ps 0.5 lc rgb "blue"
set style line 3 linetype 2 linewidth 2 pt 3 ps 0.5 lc rgb "green"
set style line 4 linetype 2 linewidth 2 pt 3 ps 0.5 lc rgb "cyan"
set style line 5 linetype 2 linewidth 2 pt 3 ps 0.5 lc rgb "dark-violet"
# function to calculate difference to previous value
delta_v(x) = ( vD = x - old_v, old_v = x, vD)
old_v = NaN
# ($2) used to reference value of column 2
plot filename  using 1:2 ls 5 title 'confirmed', filename using 1:3 ls 2 title 'recovered', filename using 1:($2-$3) ls 3 title 'new infections', filename using 1:(delta_v($2)) with boxes fill title 'Delta' axes x1y2, filename using 1:4 ls 1 title 'deaths' axes x1y2
