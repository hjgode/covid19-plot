# "time_series_covid19_germany_transposed.csv"
# gnuplot -e "filename='time_series_germany_sorted.csv';outfile='germany.png';mytitle='Germany'" plot_covid.gplot

if (!exists("filename")) filename='time_series_germany_sorted.csv'
if (!exists("outfile")) outfile='germany.png'
if (!exists("mytitle")) mytitle='Germany'
set title mytitle
set xdata time 
set timefmt "%m/%d/%Y"
set style data lines
set terminal png size 1000,400
set autoscale y
set output outfile
set datafile separator ","
set xrange ["3/1/20":*]
set y2tics nomirror
set y2tics
set y2range [0:10000]
set y2label 'Delta'
# set legend position
set key left bottom
# function to calculate difference to previous value
delta_v(x) = ( vD = x - old_v, old_v = x, vD)
old_v = NaN
# ($2) used to reference value of column 2
plot filename  using 1:2 with lines title 'confirmed', filename using 1:3 with lines title 'recovered', filename using 1:(delta_v($2)) with boxes fill title 'Delta' axes x1y2
